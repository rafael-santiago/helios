#		                          Thu 16 Jul 2015 09:42:14 AM BRT
# "lnxkmod_forge.hsl"
#
#	by Rafael Santiago
#
# Description: Linux kernel module forge stuff.
#
include ~/fsutil.hsl

function lnxkmod_forge(main_src_file type string,
                       includes type list,
                       cflags type list,
                       libraries type list,
                       ldflags type list,
                       kmod_name type string) : result type int {
    var src_files type list;

    $src_files.ls(".*\\.c$");
#    if ($src_files.count() == 0) {
#	result 1;
#    }

    if ($includes.count() > 0) {
        var cwd type string;
        $cwd = hefesto.sys.pwd();

        var i type int;
        $i = 0;
        while ($i < $includes.count()) {
            if (hefesto.sys.cd($includes.item($i))) {
                $src_files.ls(".*\\.c$");
                hefesto.sys.cd($cwd);
            }
            $i = $i + 1;
        }
    }

    var exit_code type int;
    $exit_code = 1;
    mkmakefile($src_files, $main_src_file, $includes, $cflags, $libraries, $ldflags, $kmod_name);
    $exit_code = hefesto.sys.run(hefesto.toolset.mk_kmodule());

    cleanup_kbuild_files();

    if ($exit_code == 0) {
	hefesto.sys.echo("\n\n___\n*** compilation success.\n");
    } else {
	hefesto.sys.echo("\n\n___\n~~~ compilation failure.\n");
    }

    result $exit_code;
}

local function get_path_deepness_level(path type string) : result type int {
    var p type int;
    $p = 0;

    var lv type int;
    $lv = 0;
    while ($p < $path.len()) {
        if ($path.at($p) == "/") {
            $lv = $lv + 1;
        }
        $p = $p + 1;
    }

    result $lv;
}

local function get_indirect_file_path(path type string, indirect_delta type int) : result type string {
    var indirect_path type string;

    if ($indirect_delta > 0) {
        $indirect_path = hefesto.sys.make_path("./", "..");
        while ($indirect_delta > 1) {
            $indirect_path = hefesto.sys.make_path($indirect_path, "..");
            $indirect_delta = $indirect_delta - 1;
        }
    }
    result hefesto.sys.make_path($indirect_path, filenamefrompath($path));
}

local function mkmakefile(srcs type list,
                          main type string,
                          includes type list,
                          cflags type list,
                          libraries type list,
                          ldflags type list,
                          kmod_name type string) : result type none {
    var fp type file;
    $fp = hefesto.sys.fopen("Makefile", "w");
    var objname type string;
    var line type string;
    var s type int;
    var pwd_lv type int;
    var src_lv type int;

    $pwd_lv = get_path_deepness_level(hefesto.sys.pwd() + "/");

    $s = 0;

    $line = "obj-m += " + $kmod_name + ".o\n";
    hefesto.sys.fwrite($line, $line.len(), $fp);

    $line = $kmod_name + "-objs := ";

    var cwd_regex type string;
    $cwd_regex = "^" + hefesto.sys.pwd() + "/";

    while ($s < $srcs.count()) {
        $objname = $srcs.item($s);
        $src_lv = get_path_deepness_level($objname);
        if ($objname.replace($cwd_regex, "") > 0) {
            $objname = "./" + $objname;
        } else if ($pwd_lv > $src_lv) {
            $objname = get_indirect_file_path($objname, $pwd_lv - $src_lv);
        }
        $objname.replace("\\.c$", ".o");
        $line = $line + $objname + " ";
        $s = $s + 1;
    }

    var item type string;

    $s = 0;
    while ($s < $ldflags.count()) {
        $item = $ldflags.item($s);
        if ($item.match(".*a$")) {
            $line = $line + $item + " ";
        }
        $s = $s + 1;
    }

    $line = $line + "\n";

    if ($includes.count() > 0 || $cflags.count() > 0) {
        $line = $line + "ccflags-y := ";
        $s = 0;
        while ($s < $includes.count()) {
            $line = $line + "-I" + $includes.item($s) + " ";
            $s = $s + 1;
        }

        $s = 0;
        while ($s < $cflags.count() > 0) {
            $line = $line + $cflags.item($s) + " ";
            $s = $s + 1;
        }

        $line = $line + "\n";
    }

    if ($libraries.count() > 0 || $ldflags.count() > 0) {
        $line = $line + "ldflags-y := ";

        $s = 0;
        while ($s < $libraries.count()) {
            $line = $line + "-L" + $libraries.item($s) + " ";
            $s = $s + 1;
        }

        $s = 0;
        while ($s < $ldflags.count()) {
            $item = $ldflags.item($s);
            if ($item.match("^-")) {
                $line = $line + $item + " ";
            }
            $s = $s + 1;
        }
        $line = $line + "\n";
    }

    hefesto.sys.fwrite($line, $line.len(), $fp);
    $line = "all:\n\tmake -C /lib/modules/$(shell uname -r)/build M=$(PWD) modules\nclean:\n\tmake -C /lib/modules/$(shell uname -r)/build M=$(PWD) clean\n";
    hefesto.sys.fwrite($line, $line.len(), $fp);
    hefesto.sys.fclose($fp);
}

local function cleanup_kbuild_files() : result type none {
    hefesto.sys.rm("Makefile");
    hefesto.sys.rm("Module.symvers");
    hefesto.sys.rm("modules.order");
    hefesto.sys.rm("Kbuild");
}
