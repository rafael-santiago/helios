#
#                              Copyright (C) 2014 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#
# "vc110_lib_forge.hsl"
#       by Rafael Santiago
#
include ~/toolsets/vc/vc110-lib.hsl
include ~/toolsets/vc/forges/common/vcutils.hsl
include ~/toolsets/vc/vcvarsall/vcvarsall_110.hsl
include ~/fsutil.hsl
include ~/lsutil.hsl

function vc110_lib_forge(sources type list, includepaths type list,
                         cflags type list, libpaths type list,
                         ldflags type list, libname type string) : result type int {

    var exit_code type int;
    $exit_code = 0;

    #
    # Try to detect the cpu architecture.
    #
    # If you need to specify an architecture use --cpu-arch=<arch> option.
    #

    $exit_code = vc110_vcvarsall("");

    if ($exit_code != 0) {
        hefesto.sys.echo(hefesto.project.toolset() + " critical error: unable to configure VC110 environment!\n");
        result $exit_code;
    }

    if ($sources.count() == 0) {
        $sources.ls(".*\\.(c|cc|cpp|CC|Cpp|CPP)$");
    }

    if ($sources.count() == 0) {
        hefesto.sys.echo(hefesto.project.toolset() + " ERROR: empty source list!\n");
        result 1;
    }

    var obj_output_dir type list;
    var obj_dir type string;

    $obj_output_dir = hefesto.sys.get_option("obj-output-dir");

    if ($obj_output_dir.count() > 0) {
        $obj_dir = $obj_output_dir.item(0);
        mktree($obj_dir);
    } else {
        $obj_dir = hefesto.sys.pwd();
    }

    var bin_output_dir type list;
    var bin_output type string;

    $bin_output_dir = hefesto.sys.get_option("bin-output-dir");

    if ($bin_output_dir.count() > 0) {
        $bin_output = $bin_output_dir.item(0);
        mktree($bin_output);
        $bin_output = hefesto.sys.make_path($bin_output, $libname);
    } else {
        $bin_output = $libname;
    }

    if ($sources.count() == 0) {
        hefesto.sys.echo(hefesto.project.toolset() + " critical error: no files to process!\n");
        result 1;
    }

    var isrelease type int;
    var compilemodel type list;

    $compilemodel = hefesto.sys.get_option("compile-model");
    if ($compilemodel.count() > 0) {
        if ($compilemodel.item(0) == "release") {
            $isrelease = 1;
        } else {
            if ($compilemodel.item(0) == "debug") {
                $isrelease = 0;
            } else {
                hefesto.sys.echo(hefesto.project.toolset() + " critical error: unknown compile-model, must be release or debug.\n");
                result 1;
            }
        }
    } else {
        $isrelease = 1;
    }

    var isstatic type int;
    var linkmodel type list;

    $linkmodel = hefesto.sys.get_option("link-model");
    if ($linkmodel.count() != 0) {
        if ($linkmodel.item(0) == "static") {
            $isstatic = 1;
        } else {
            if ($linkmodel.item(0) == "shared") {
                $isstatic = 0;
            } else {
                hefesto.sys.echo(hefesto.project.toolset() + " critical error: unknown link-model, must be static or shared.\n");
                result 1;
            }
        }
    } else {
        $isstatic = 1;
    }

    var includepaths_str type string;
    var libpaths_str type string;
    var ldflags_str type string;
    var cflags_str type string;

    $includepaths_str = vcincs2string($includepaths);
    $libpaths_str = vclibs2string($libpaths);
    $ldflags_str = vcflags2string($ldflags);
    $cflags_str = vcflags2string($cflags);

    var s type int;

    hefesto.sys.echo("*** Compiling... please wait...\n");

    # compiling...

    $s = 0;

    var qsize type list;
    var qsize_nr type int;

    $qsize = hefesto.sys.get_option("qsize");
    if ($qsize.count() > 0) {
        $qsize_nr = $qsize.item(0);
    } else {
        $qsize_nr = 1;
    }

    var curr_src_file type string;
    var not_compiled type list;
    var compilation_nr type int;

    $compilation_nr = 0;
    $not_compiled =lsdup($sources);

    if ($qsize_nr == 1) {
        while ($s < $sources.count() && $exit_code == 0) {
            $curr_src_file = $sources.item($s);
            if (vc_should_compile($curr_src_file) == 1) {
                $exit_code = vc_sync_compile_src($curr_src_file,
                                                 $includepaths_str,
                                                 $cflags_str,
                                                 $obj_dir,
                                                 $isrelease,
                                                 $isstatic);
                if ($exit_code == 0) {
                    $not_compiled.del_item($curr_src_file);
                    $compilation_nr = $compilation_nr + 1;
                    hefesto.sys.echo("\t" + $sources.item($s) + " is ok.\n");
                } else {
                    hefesto.sys.echo("\t" + $sources.item($s) + " has error(s).\n");
                }
            } else {
                hefesto.sys.echo("\t" + $sources.item($s) + " is updated.\n");
            }
            $s = $s + 1;
        }
        if ($compilation_nr > 0) {
            hefesto.toolset.base_refresh($not_compiled);
        }
    } else {
        $exit_code = vc_async_compile_srcs($sources,
                                           $includepaths_str,
                                           $cflags_str,
                                           $obj_dir,
                                           $isrelease,
                                           $isstatic);
    }

    if ($exit_code == 0) {
        hefesto.sys.echo("*** All sources were compiled.\n\n*** Linking...\n");
        var binary type string;
        $binary = $bin_output;
        if (($binary.match("\\.lib$") > -1 || $binary.match("\\.a$") > -1) &&
            $isstatic == 0) {
            $binary = vc110_get_libname($binary) + ".dll";
        }
        if (isfile($binary) == 0 && $compilation_nr == 0) {
            $compilation_nr = 1;
        }
        if ($compilation_nr > 0) {
            # linking...
            $exit_code = vc_link_objects2lib($sources,
                                             $obj_dir,
                                             $libpaths_str,
                                             $ldflags_str,
                                             $binary,
                                             $isrelease,
                                             $isstatic);
            if ($exit_code == 0) {
                hefesto.sys.echo("*** Link success (output:" + $binary + ")\n");
            } else {
                hefesto.sys.echo("*** Link error\n");
            }
        }
    } else {
        hefesto.sys.echo("*** Errors were found.\n");
    }

    result $exit_code;
}

function vc_link_objects2lib(sources type list,
                             obj_dir type string,
                             libs type string,
                             ldflags type string, libname type string,
                             isrelease type int, isstatic type int) : result type int {
    var cmd type string;
    var objects type string;
    var retcode type int;
    $objects = vc_objs_from_srcs($sources, $obj_dir);
    $retcode = 1;
    if ($isstatic == 1) {
        $cmd = hefesto.toolset.lib($libname, $objects, $libs, $ldflags);
        $retcode = hefesto.sys.run($cmd);
        if ($isrelease == 0 && $retcode == 0) {
            $retcode = vc110_postprocess_pdb($libname);
        }
    } else {
        if ($isrelease == 1) {
            $cmd = hefesto.toolset.dll_release($libname, $objects, $libs, $ldflags);
        } else {
            $cmd = hefesto.toolset.dll_debug($libname, $objects, $libs, $ldflags);
        }
        $retcode = hefesto.sys.run($cmd);
    }
    result $retcode;
}

function vc110_get_libname(artifact type string) : result type string {
    var al type int;
    var a type int;

    $al = $artifact.len() - 1;

    while ($al > 0 && $artifact.at($al) != ".") $al = $al - 1;

    var return type string;

    $a = 0;
    $return = "";

    while ($a < $al) {
        $return = $return + $artifact.at($a);
        $a = $a + 1;
    }

    result $return;
}

function vc110_postprocess_pdb(filename type string) : result type int {
    var retcode type int;   
    if (hefesto.sys.ls("vc110\\.pdb") == 0) {
        result 1;
    }
    var pdbfile type string;
    $retcode = 1;
    $pdbfile = vc110_get_libname($filename) + ".pdb";
    if (hefesto.sys.cp("vc110.pdb", $pdbfile) == 1) {
        if (hefesto.sys.rm("vc110.pdb") == 0) {
            $retcode = 0;
        }
    }
    result $retcode;
}
