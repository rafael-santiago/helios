#
#                              Copyright (C) 2014 by Rafael Santiago
#
# This is free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
# "forge_projects.hsl"
#

include ~/fsutil.hsl

#
# This function recursivelly calls the default forge of projects
# which are under passed cwd.
#
# @cwd = a list containing all initial directories  that  must
#        be scanned for projects with a configurated .ivk file
#

function forge_projects(cwd type list) : result type int {
    var exit_code type int;
    var dirs type list;
    var c type int;
    var old_cwd type string;
    var new_cwd type string;
    $exit_code = 0;
    $c = 0;
    while ($c < $cwd.count() && $exit_code == 0) {
        $old_cwd = hefesto.sys.pwd();
        $new_cwd = $cwd.item($c);
        hefesto.sys.cd($new_cwd);
        if (hefesto.sys.ls("\\.ivk$") == 1) {
            hefesto.sys.echo("forge_project() current status: entering inside \"" + $new_cwd + "\" directory...\n");
            $exit_code = hefesto.sys.run("hefesto " + hefesto.project.cmdline());
            hefesto.sys.echo("\n\nforge_project() current status: looking for next sub-project...\n\n");
        }
        if ($exit_code == 0) {
            $dirs = lsdir();
            if ($dirs.count() > 0) {
                $exit_code = forge_projects($dirs);
            }
        }
        hefesto.sys.cd($old_cwd);
        $c = $c + 1;
    }
    result $exit_code;
}
